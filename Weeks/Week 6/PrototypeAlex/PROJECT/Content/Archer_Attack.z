class Archer_Attack : SuperState
{
    //PROPERTIES
    [Property]
    var Duration : Real = 10.0;
    [Property]
    var MaxDistance : Real = 40.0;
    [Property]
    var MeleeDistance : Real = 5;
    [Property]
    var ShootingMaxDistance : Real = 25;
    
    //OTHERVARIABLES
    var ShootingMinDistance : Real = 5;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
    }
    [Override] function Update(event : UpdateEvent)
        {
            //Setting some variables to define the distance to the player
            var playerPos : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPos : Real3 = this.Actor.Transform.Translation;
            var distanceToPlayer : Real = Math.Distance( ownerPos, playerPos );
            
            if (distanceToPlayer <= this.ShootingMaxDistance && distanceToPlayer >= this.ShootingMinDistance)   /*Going to attack from distance*/
            {
                this.Owner.StateMachine.ChangeState(this.Owner.Archer_Attack_DistanceA);
            }
            else if  (distanceToPlayer <= this.MeleeDistance)                                                   /*Going to attack from melee*/
            {
                this.Owner.StateMachine.ChangeState(this.Owner.Archer_Attack_MeleeA);
            }
            else if (distanceToPlayer >= this.ShootingMaxDistance && distanceToPlayer <this.MaxDistance)        /*Going to Patrol*/
            {
                this.Owner.StateMachine.FindStateMachineFromRoot("ArcherAI").ChangeState("Archer_SeekTarget");
            }
            else if (distanceToPlayer >= this.MaxDistance)                                                      /*Going to Patrol*/
            {
                this.Owner.StateMachine.FindStateMachineFromRoot("ArcherAI").ChangeState("Archer_SeekTarget");
            }
        }
}
/*-------------------------------------------------------------------------------------------------------------------------------------------------*/
class Archer_Attack_DistanceA : State /* The Enemy stops and starts shooting arrows at the Player*/
{
    //DEPENDENCIES
    [Dependency]
    var Transform : Transform;
    
    //PROPERTIES
    [Property] 
    var FireRate : Real = 1.0;
    [Property] 
    var BulletArchetype : Archetype = Archetype.Arrow;
    [Property] 
    var BulletSpeed : Real = 20.0;
    [Property]
    var MeleeDistance : Real = 5.0;
    [Property]
    var ShootingMinDistance : Real = 5;
    [Property]
    var LosingDistance : Real = 25;
    
    //OTHERVARIABLES
    var Timer : Real = 0.0;
    var ShootingAngle : Real;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
    }
    [Override] function Update(event: UpdateEvent)
        {
            this.Timer += event.Dt;
            
            //Setting variables that wiil be needed to compute the distance to player and the angle for the Fire() function
            var playerPosition : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPosition : Real3 = this.Actor.Transform.Translation;
            var vectorToPlayer = playerPosition - ownerPosition;
            var distanceToPlayer = Math.Distance(playerPosition, ownerPosition);
            
            this.ShootingAngle = Math.ATan2(vectorToPlayer.Y, vectorToPlayer.X);
            
            if (this.Timer >= this.FireRate) /*Setting a FireRate*/
            {
                this.Fire();
                this.Timer = 0;
            } 
            if (distanceToPlayer >= this.LosingDistance)    /* If the player is out of range, get closer*/
            {
                this.Owner.StateMachine.FindStateMachineFromRoot("ArcherAI").ChangeState("Archer_SeekTarget");
            }
            else if(distanceToPlayer <= this.MeleeDistance) /*If the player is too close, start attacking him melee*/
            {
                this.Owner.StateMachine.ChangeState("Archer_Attack_MeleeA");
            }
        }
    function Fire() /*This function enables the shooting of the archer*/
    {
        var bulletPosition = this.Owner.Transform.WorldTranslation;
        var bullet = this.Space.CreateAtPosition(this.BulletArchetype, bulletPosition);
        var firingAngle = this.ShootingAngle;
        
        var xPart = Math.Cos(firingAngle);
        var yPart = Math.Sin(firingAngle);
        var zPart = 0.0;
        
        var firingDirection = Real3(xPart, yPart, zPart);
        var firingAng : Real3 = Real3(0, 0, 0);
        
        //firingAngle = Math.ToDegrees(radians :firingAngle);
        //"I needed the angles on degrees, keeping it just in case"
        
        firingAng = Real3(0, 0, firingAngle + Math.Pi);
        bullet.Transform.EulerAngles = firingAng;
        bullet.RigidBody.Velocity = firingDirection * this.BulletSpeed;
    }
}
/*-------------------------------------------------------------------------------------------------------------------------------------------------*/
class Archer_Attack_MeleeA : State
{
    //PROPERTIES
    [Property]
    var MeleeDistance : Real = 5.0;
    [Property]
    var Range : Real = 3.0;
    [Property]
    var MeleeAttackRate : Real = 1.0;
    
    //OTHERVARIABLES
    var AttackTime : Real = 0.0;
    var MovementStateMachine : StateMachine = null;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
        this.MovementStateMachine = this.Owner.StateMachine.FindStateMachineFromRoot("Movement"); 
        /* I needed to get access to the Player movement because the player could be out of range but still be in
        melee attack range, and I didn't want those to be the same*/
    }
    [Override] function Update (event : UpdateEvent)
        {
            /*The attack is set to have a cooldown, and it works with a BoxCollider that starts a CollisionPersisted 
            event only when the player is in range, this event will send a message to the player's health him 
            that he has been hit*/
            
            this.Owner.FindChildByName("DamageBox").BoxCollider.SendsEvents = false;/*In order to only have one Frame sent*/
            this.AttackTime += event.Dt;
            
            //Setting some variables for the Player distance
            var playerPosition : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPosition : Real3 = this.Actor.Transform.Translation;
            var distanceToPlayer = Math.Distance(playerPosition, ownerPosition);
            
            if (distanceToPlayer <= this.Range && this.AttackTime >= this.MeleeAttackRate)      /*Attack the player*/
            {
                this.MovementStateMachine.ChangeState("Move_Idle");
                this.Owner.FindChildByName("DamageBox").BoxCollider.SendsEvents = true;
                this.AttackTime = 0;
            }
            else if (distanceToPlayer >= this.MeleeDistance)                                    /*Going back to distance attacks */
            {
                this.MovementStateMachine.ChangeState("Move_Idle");
                this.Owner.StateMachine.ChangeState("Archer_Attack_DistanceA");
            }
            else if (distanceToPlayer >= this.Range && distanceToPlayer <= this.MeleeDistance)  /*Getting closer to the player JIC*/
            {
                this.MovementStateMachine.ChangeState("Move_Walk");
            }
        }
}
/*-------------------------------------------------------------------------------------------------------------------------------------------------*/