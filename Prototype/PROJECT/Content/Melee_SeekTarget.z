class Melee_SeekTarget : SuperState
{
    [Property]
    var MaxDistance : Real = 40; 
    [Property]
    var MeleeDistance : Real = 4.0;
    [Property] 
    var SensorCogPath : CogPath = null;
    
    
    var SensorCog : Cog = null;
    var TargetCog : Cog = null;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
            
        if (this.SensorCogPath != null)
            this.SensorCog = this.SensorCogPath.Cog;
    }
    
    [Override]function Enter()
        {
            //Setting some variables to compute the distance form the owner to the player
            var playerPos : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPos : Real3 = this.Actor.Transform.Translation;
            var distanceToPlayer : Real = Math.Distance( ownerPos, playerPos );
            
            //if you enter the Melee_SeekTarget this will decide where you go
            if(distanceToPlayer <= this.MaxDistance && distanceToPlayer >= this.MeleeDistance)          /*Chasing the Player in the distance*/
                this.Owner.StateMachine.ChangeState(this.Owner.Melee_SeekTarget_Chase);
            
            if (distanceToPlayer >= this.MaxDistance)                                                   /*Losing the Player bcs the distance not respected*/
                this.Owner.StateMachine.ChangeState(this.Owner.Melee_SeekTarget_Lost);
                
            if(distanceToPlayer <= this.MeleeDistance)
            {
                this.Owner.StateMachine.FindStateMachineFromRoot("MeleeAI").ChangeState("Melee_Attack");
            }
            this.TargetCog = this.Owner.Melee_Patrol.TargetCog;
        }
    [Override]function Update(event:UpdateEvent)
        {
        }
}
class Melee_SeekTarget_Chase : State
{
    [Property]
    var MaxDistance : Real = 40; 
    [Property]
    var MeleeDistance : Real = 4.0;
    
    var MovementStateMachine : StateMachine = null;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
        this.MovementStateMachine = this.Owner.StateMachine.FindStateMachineFromRoot("Movement");
    }
    [Override] function Update(event : UpdateEvent)
        {
            if (this.Owner.Parent.Parent.Parent.EnemyHealth.BeenDamaged == true)
                this.Owner.StateMachine.FindStateMachineFromRoot("MeleeAI").ChangeState("Melee_Stuned");
            
            //setting some variables to compute the distance
            var playerPos : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPos : Real3 = this.Actor.Transform.Translation;
            var distanceToPlayer : Real = Math.Distance( ownerPos, playerPos );
            var target = this.Owner.Parent.Melee_SeekTarget.TargetCog;
            var direc = Real3(0,0,0);
            
            if(target != null)
                direc = target.Transform.Translation - this.Actor.Transform.Translation;
                
            
            this.MovementStateMachine.Owner.Move_Walk.Direction = Math.Sign(direc.X);
            this.MovementStateMachine.ChangeState("Move_Walk");
            
            if (distanceToPlayer <= this.MaxDistance && distanceToPlayer >= this.MeleeDistance)
                {
                    this.MovementStateMachine.ChangeState("Move_Walk");
                }
            else if (distanceToPlayer <= this.MeleeDistance)
                {
                    this.MovementStateMachine.ChangeState("Move_Idle");
                    this.Owner.StateMachine.FindStateMachineFromRoot("MeleeAI").ChangeState("Melee_Attack");
                }
            else if (distanceToPlayer >= this.MaxDistance)
                {
                    this.Owner.StateMachine.ChangeState(this.Owner.Melee_SeekTarget_Lost);
                }
        }
}
class Melee_SeekTarget_Lost : State
{
    [Property] 
    var Duration : Real = 5.0;
    
    var FlipTimer : Real = 1.0;
    
    var MovementStateMachine : StateMachine = null;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
        this.MovementStateMachine = this.Owner.StateMachine.FindStateMachineFromRoot("Movement");
    }
    
    [Override] function Enter()
    {
        this.MovementStateMachine.ChangeState("Move_Idle");
        Zero.Connect(this.Owner.Parent.Melee_SeekTarget.SensorCog, Events.CollisionStarted, this.OnSensorEnter);
    }
    [Override] function Exit()
    {
        Zero.Disconnect(this.Owner.Parent.Melee_SeekTarget.SensorCog, Events.CollisionStarted, this);
    }
    function OnSensorEnter(event : CollisionEvent)
    {
        this.Owner.StateMachine.ChangeState(this.Owner.Melee_SeekTarget_Chase);
    }
    [Override] function Update(event : UpdateEvent)
    {
        if (this.Owner.Parent.Parent.Parent.EnemyHealth.BeenDamaged == true)
                this.Owner.StateMachine.FindStateMachineFromRoot("MeleeAI").ChangeState("Melee_Stuned");
        
        
        if(this.TimeInState > this.Duration)
            this.Owner.StateMachine.FindStateMachineFromRoot("MeleeAI").ChangeState("Melee_Patrol");
            
        this.FlipTimer += event.Dt;
        if(this.FlipTimer > this.Duration * 0.25) 
        {
            this.Actor.Sprite.FlipX = !this.Actor.Sprite.FlipX;
            this.FlipTimer = 0.0;   
        }
    }
}
