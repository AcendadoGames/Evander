class Archer_Attack : SuperState
{
    //PROPERTIES
    [Property]
    var Duration : Real = 10.0;
    [Property]
    var MaxDistance : Real = 40.0;
    [Property]
    var MeleeDistance : Real = 5;
    [Property]
    var ShootingMaxDistance : Real = 25;
    
    //OTHERVARIABLES
    var ShootingMinDistance : Real;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
    }
    
    //This is just to set the minimum of confusion when it comes to this code.
    [Override] function Enter()
        {
            this.ShootingMinDistance = this.MeleeDistance;
        }
        
    [Override] function Update(event : UpdateEvent)
        {
            //Setting some variables to define the distance to the player
            var playerPos : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPos : Real3 = this.Actor.Transform.Translation;
            var distanceToPlayer : Real = Math.Distance( ownerPos, playerPos );
            
            if (distanceToPlayer <= this.ShootingMaxDistance && distanceToPlayer >= this.ShootingMinDistance)   /*Going to attack from distance*/
            {
                this.Owner.StateMachine.ChangeState(this.Owner.Archer_Attack_DistanceA);
            }
            else if  (distanceToPlayer <= this.MeleeDistance)                                                   /*Going to attack from melee*/
            {
                this.Owner.StateMachine.ChangeState(this.Owner.Archer_Attack_MeleeA);
            }
            else if (distanceToPlayer >= this.ShootingMaxDistance && distanceToPlayer <this.MaxDistance)        /*Going to Patrol*/
            {
                this.Owner.StateMachine.FindStateMachineFromRoot("ArcherAI").ChangeState("Archer_SeekTarget");
            }
            else if (distanceToPlayer >= this.MaxDistance)                                                      /*Going to Patrol*/
            {
                this.Owner.StateMachine.FindStateMachineFromRoot("ArcherAI").ChangeState("Archer_SeekTarget");
            }
        }
}
/*-------------------------------------------------------------------------------------------------------------------------------------------------*/
class Archer_Attack_DistanceA : State /* The Enemy stops and starts shooting arrows at the Player*/
{
    //DEPENDENCIES
    [Dependency]
    var Transform : Transform;
    
    //PROPERTIES
    [Property] 
    var FireRate : Real = 1.0;
    [Property] 
    var BulletArchetype : Archetype = Archetype.Arrow;
    [Property] 
    var BulletSpeed : Real = 20.0;
    [Property]
    var MeleeDistance : Real = 5.0;
    [Property]
    var LosingDistance : Real = 25;
    
    //OTHERVARIABLES
    var ShootingMinDistance : Real;
    var Timer : Real = 0.0;
    var ShootingAngle : Real;
    var MuzzleObject : Cog = null;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
        this.MuzzleObject = this.Owner.FindChildByName("Muzzle");
    }
    
    [Override] function Enter()
        {
            this.ShootingMinDistance = this.MeleeDistance;
        }
        
    [Override] function Update(event: UpdateEvent)
        {
            this.Timer += event.Dt;
            
            //Setting variables that wiil be needed to compute the distance to player and the angle for the Fire() function
            var playerPosition : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPosition : Real3 = this.Actor.Transform.Translation;
            var vectorToPlayer = playerPosition - ownerPosition;
            var distanceToPlayer = Math.Distance(playerPosition, ownerPosition);
            
            
            this.ShootingAngle = Math.ATan2(vectorToPlayer.Y, vectorToPlayer.X);
            
            if (this.Timer >= this.FireRate) /*Setting a FireRate*/
            {
                this.Fire();
                this.Timer = 0;
            } 
            if (distanceToPlayer >= this.LosingDistance)    /* If the player is out of range, get closer*/
            {
                this.Owner.StateMachine.FindStateMachineFromRoot("ArcherAI").ChangeState("Archer_SeekTarget");
            }
            else if(distanceToPlayer <= this.MeleeDistance) /*If the player is too close, start attacking him melee*/
            {
                this.Owner.StateMachine.ChangeState("Archer_Attack_MeleeA");
            }
        }
        function Fire()
        {
            //TimeInTheAirToTarget
            var t : Real = 0.8;
            //DistanceToPlayer
            var distanceToTarget: Real = 0.0;
            //OwnerPosition
            var ownerPosition: Real3 = this.StateMachine.Actor.Transform.Translation;
            //Finding to Player
            var target : Cog = this.Space.FindObjectByName("PrototypePlayer");
            //The shooting Angle
            var alpha : Real = 0.0;
            //Adding the velocity vector
            var velocity : Real = 0.0;
            
            //Bullet Information
            var bulletPosition = this.MuzzleObject.Transform.WorldTranslation;
            var bullet = this.Space.CreateAtPosition(this.BulletArchetype, bulletPosition);
            
            //Setting the Initial velocity
            var iniVelocity : Real3 = Real3(0, 0, 0);
            
            //Getting the games gravity
            var gravity = this.Space.LevelSettings.GravityEffect.Strength;
            

                //Computing distance to player
                distanceToTarget = Math.Distance(ownerPosition, target.Transform.Translation);
                
                //Modifing the time in the aire depending on the distanceToTraget
                t *= distanceToTarget / 30;
                
                //Getting the angle with the parabolic equations
                alpha = Math.ATan((target.Transform.Translation.Y - ownerPosition.Y - 
                                    (-gravity * t * t) / 2) / (target.Transform.Translation.X  
                                    - (target.RigidBody.Velocity.X * t) - ownerPosition.X));
                
                //Finding the velocity with the angle
                velocity = ((target.Transform.Translation.X - ownerPosition.X) / (t * Math.Cos(alpha)));
                
                
                //INFORMATION WRITELINES
                /*
                Console.WriteLine("PlayerDistance `distanceToTarget`");
                Console.WriteLine(t);
                Console.WriteLine("The angle is `Math.ToDegrees(radians :alpha)`");
                Console.WriteLine("The velocity is `velocity`");
                Console.WriteLine("The distance to target`distanceToTarget`");
                Console.WriteLine("--------------------------------");
                */
                
                //Modifing the inicial velocity
                iniVelocity.X = Math.Cos(alpha) * velocity;
                iniVelocity.Y = Math.Sin(alpha) * velocity;
                
                //Creating the Bullet
                bullet.RigidBody.Velocity = iniVelocity;
            
        }
}
/*-------------------------------------------------------------------------------------------------------------------------------------------------*/
class Archer_Attack_MeleeA : State
{
    //PROPERTIES
    [Property]
    var MeleeDistance : Real = 3.0;
    [Property]
    var Range : Real = 3.0;
    [Property]
    var MeleeAttackRate : Real = 1.0;
    
    //OTHERVARIABLES
    var AttackTime : Real = 0.0;
    var MovementStateMachine : StateMachine = null;
    var Direction : Real;
    
    function Initialize(init : CogInitializer)
    {
        this.StateInitialize(init);
        this.MovementStateMachine = this.Owner.StateMachine.FindStateMachineFromRoot("Movement"); 
        
        /* I needed to get access to the Player movement because the player could be out of range but still be in
        melee attack range, and I didn't want those to be the same*/
    }
    [Override] function Update (event : UpdateEvent)
        {
            /*The attack is set to have a cooldown, and it works with a BoxCollider that starts a CollisionPersisted 
            event only when the player is in range, this event will send a message to the player's health him 
            that he has been hit*/
            this.Owner.FindChildByName("DamageBox").BoxCollider.SendsEvents = false; /*In order to only have one Frame sent*/
            
            this.AttackTime += event.Dt;
            
            //Setting some variables for the Player distance
            var playerPosition : Real3 = this.Space.FindObjectByName("PrototypePlayer").Transform.Translation;
            var ownerPosition : Real3 = this.Actor.Transform.Translation;
            var distanceToPlayer = Math.Distance(playerPosition, ownerPosition);
            var target = this.Space.FindObjectByName("PrototypePlayer");
            
            var direc = target.Transform.Translation - this.Actor.Transform.Translation;
            
            
            
            //Just incase the player jump over the Archer
            this.MovementStateMachine.Owner.Move_Walk.Direction = Math.Sign(direc.X);
            
                                //The actual Attacking    
                if (distanceToPlayer <= this.Range && this.AttackTime >= this.MeleeAttackRate)      /*Attack the player*/
                {
                    this.MovementStateMachine.ChangeState("Move_Idle");
                    this.Owner.FindChildByName("DamageBox").BoxCollider.SendsEvents = true;
                    this.AttackTime = 0;
                }
                else if (distanceToPlayer >= this.MeleeDistance)                                    /*Going back to distance attacks */
                {
                    this.MovementStateMachine.ChangeState("Move_Idle");
                    this.Owner.StateMachine.ChangeState("Archer_Attack_DistanceA");
                }
                else if (distanceToPlayer >= this.Range && distanceToPlayer <= this.MeleeDistance)  /*Getting closer to the player JIC*/
                {
                    this.MovementStateMachine.ChangeState("Move_Walk");
                }
        }
        
    [Override] function Exit()
        {
            //we are setting it to 0 again if not, it will restart at the last AttackTime if it leaves
            this.AttackTime = 0;
        }
}
/*-------------------------------------------------------------------------------------------------------------------------------------------------*/